# Maintainer (of this modified fork): Lucas S. Beiler <lucasbeiler@protonmail.com>
# Forked from: https://aur.archlinux.org/packages/brave-bin/

## Maintainers of the original package from Arch's AUR:
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jacob Mischka <jacob@mischka.me>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>
# Contributor: Simón Oroño <simonorono@protonmail.com>
# Contributor: now-im <now im 627 @ gmail . com>
# Contributor: Giusy Digital <kurmikon at libero dot it>
# Contributor: Andrés Rodríguez <hello@andres.codes>

pkgname=brave-bin
pkgver=1.18.77
pkgrel=1
epoch=1
pkgdesc="Web browser that blocks ads and trackers by default (binary release)."
arch=("x86_64")
url="https://brave.com/download"
license=("MPL2" "BSD" "custom:chromium")
depends=("gtk3" "nss" "alsa-lib" "libxss" "ttf-font")
optdepends=("cups: Printer support"
            "pepper-flash: Adobe Flash support"
            "libgnome-keyring: Enable GNOME keyring support"
            "libnotify: Native notification support")
provides=("${pkgname%-bin}" "brave-browser")
conflicts=("${pkgname%-bin}")
source=("$pkgname-$pkgver.zip::https://github.com/brave/brave-browser/releases/download/v${pkgver}/brave-browser-${pkgver}-linux-amd64.zip"
        "$pkgname.sh")
options=(!strip)
sha512sums=("e9653b1f5c19a897f377028a651f025a99042406ddd62fae7be9b26ce3b8e23da4bcd15f5cc5725e92d8c31980fd8ab18056d18780129434b74b0a7d3f23e6fd"
            "ff99db6b5673846f4ec0daf0ec60535921166f6185131d8d966452452393580f655c421e7ad5889f5a6f6cb3f1e76e7908d711aa003ee37a1ae50774371a8b53")
noextract=("$pkgname-$pkgver.zip")

prepare() {
  mkdir -p brave
  bsdtar -xf $pkgname-$pkgver.zip -C brave
  chmod +x brave/brave
}

_bsdtardir="brave"

package() {
    install -d -m0755 "$pkgdir/usr/lib"
    cp -a --reflink=auto $_bsdtardir "$pkgdir/usr/lib/$pkgname"

    # allow firejail users to get the suid sandbox working
    chmod 4755 $pkgdir/usr/lib/brave-bin/chrome-sandbox

    install -Dm0755 "$pkgname.sh" "$pkgdir/usr/bin/brave"
    LICENSES_DIR="$pkgdir/usr/share/licenses/$pkgname"
    mkdir -p "$LICENSES_DIR"
    if [ -f "$pkgdir/usr/lib/$pkgname/LICENSE" ] && [ -f "$pkgdir/usr/lib/$pkgname/LICENSES.chromium.html" ]; then
      mv "$pkgdir/usr/lib/$pkgname/"{LICENSE,LICENSES.chromium.html} "$LICENSES_DIR"
    fi
}
